
Процедура ФорматироватьТабличныйДокументСДеревомВызовов(ТабличныйДокумент) Экспорт
	
	ПараметрыФорматирования = ПолучитьПараметрыФорматирования();
	
	Для сч = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Текст = ТабличныйДокумент.Область(сч, 1).Текст;
		Если Найти(Текст, ".") = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		МассивПодстрок = РазложитьСтрокуВМассивПодстрок(Текст, ".");
		ТипОбъекта = МассивПодстрок[0];
		ВидОбъекта = МассивПодстрок[1];
		
		Расположение = "$$";
		Для ИндексМассива = 2 По МассивПодстрок.ВГраница()-1 Цикл
			Расположение = Расположение + "." + МассивПодстрок[ИндексМассива];
			Если ИндексМассива = 3 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Расположение = СтрЗаменить(Расположение, "$$.", "");
		Расположение = СтрЗаменить(Расположение, "$$", "");
		
		ИмяМетода = МассивПодстрок[МассивПодстрок.Вграница()];
		
		ТабличныйДокумент.Область(сч, 1).Текст = ТипОбъекта;
		ТабличныйДокумент.Область(сч, 2).Текст = ВидОбъекта;
		ТабличныйДокумент.Область(сч, 3).Текст = Расположение;
		ТабличныйДокумент.Область(сч, 4).Текст = ИмяМетода;
		
		ФорматироватьЯчейкуПоПараметрам(ТабличныйДокумент.Область(сч, 1), ПараметрыФорматирования);
	КонецЦикла;
	
	ТабличныйДокумент.Область(1,1).ШиринаКолонки = 15;
	ТабличныйДокумент.Область(1,2).ШиринаКолонки = 30;
	ТабличныйДокумент.Область(1,3).ШиринаКолонки = 15;
	ТабличныйДокумент.Область(1,4).ШиринаКолонки = 70;
КонецПроцедуры

Функция ПолучитьПараметрыФорматирования()
	
	ПараметрыФорматирования = Новый Соответствие;
	
	Макет = ПолучитьМакет("Форматирование");
	Для сч = 1 По Макет.ВысотаТаблицы Цикл
		Область = Макет.Область(сч, 1);
		Если ПустаяСтрока(Область.Текст) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыФорматирования.Вставить(НРег(Область.Текст), Область);
	КонецЦикла;
	
	Возврат ПараметрыФорматирования;
КонецФункции

Процедура ФорматироватьЯчейкуПоПараметрам(Область, ПараметрыФорматирования)
	Образец = ПараметрыФорматирования.Получить(нрег(Область.Текст));
	Если Образец = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Область.ЦветФона = Образец.ЦветФона;
	Область.Шрифт = Образец.Шрифт;
	Область.ЦветТекста = Образец.ЦветТекста;
	
КонецПроцедуры


// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//     - для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//     - если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив - массив строк.
//
// Примеры:
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",") - возвратит массив из 5 элементов, три из которых  - пустые
//  строки;
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",", Истина) - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок(" один   два  ", " ") - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок("") - возвратит пустой массив;
//  РазложитьСтрокуВМассивПодстрок("",,Ложь) - возвратит массив с одним элементом "" (пустой строкой);
//  РазложитьСтрокуВМассивПодстрок("", " ") - возвратит массив с одним элементом "" (пустой строкой);
//
// Примечание:
//  В случаях, когда разделителем является строка из одного символа, и не используется параметр СокращатьНепечатаемыеСимволы,
//  рекомендуется использовать функцию платформы СтрРазделить.
// взято из БСП 23. СтроковыеФункцииКлиентСервер
// 
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

